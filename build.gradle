plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = "net.neoforged"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    gradlePluginPortal()
    exclusiveContent {
        forRepository {
            maven {
                name = "Fabric"
                url = "https://maven.fabricmc.net/"
            }
        }
        filter {
            includeModule("net.fabricmc", "fabric-loom-native")
        }
    }
    maven {
        name = "NeoForged"
        url = "https://maven.neoforged.net/releases"
        content {
            includeGroup "net.neoforged"
        }
    }
}

sourceSets {
    java17
    test {
        compileClasspath += sourceSets.java17.output
        runtimeClasspath += sourceSets.java17.output
    }
}

dependencies {
    java17CompileOnly gradleApi()
    java17Implementation "com.google.code.gson:gson:2.10.1"
    java17Implementation "gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1.8"
    java17Implementation "net.fabricmc:fabric-loom-native:0.2.1"
    java17Implementation "net.neoforged:JarJarMetadata:0.4.1"
    java17Implementation "com.intellij:annotations:9.0.4"
    api "gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1.8"
    api "net.neoforged:JarJarMetadata:0.4.1"

    testImplementation(enforcedPlatform("org.junit:junit-bom:5.10.2"))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

jar {
    into('META-INF/versions/17') {
        from sourceSets.java17.output
    }

    manifest.attributes(
            'Multi-Release': 'true'
    )
}

tasks.named("compileJava").configure {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

// This is a shameless ripoff from Fabric Loom:
// https://github.com/FabricMC/fabric-loom/blob/dev/1.6/build.gradle
// Need to tweak this file to pretend we are compatible with j8 so the bootstrap will run.
tasks.withType(GenerateModuleMetadata) {
    doLast {
        def file = outputFile.get().asFile

        def metadata = new groovy.json.JsonSlurper().parseText(file.text)

        metadata.variants.each {
            it.attributes["org.gradle.jvm.version"] = 8
        }

        file.text = groovy.json.JsonOutput.toJson(metadata)
    }
}

gradlePlugin {
    // Define the plugin
    plugins {
        neoforgegradle {
            id = 'net.neoforged.neoforgegradle.moddev'
            implementationClass = 'net.neoforged.neoforgegradle.ModDevPlugin'
        }
    }
}

test {
    useJUnitPlatform()
}
